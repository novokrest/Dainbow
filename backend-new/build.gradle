group 'com.oneuse.dainbow'
version '1.0.0'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE'
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
        classpath 'nu.studer:gradle-jooq-plugin:2.0.9'
//        classpath "gradle.plugin.com.boxfuse.client:flyway-release:4.0.3'
        classpath 'org.flywaydb:flyway-gradle-plugin:5.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: 'nu.studer.jooq'
apply plugin: 'org.flywaydb.flyway'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = "${rootProject.name}"
    version = "${version}"
}

repositories {
    maven {
        url 'https://maven-central.storage.googleapis.com'
    }

    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'mysql:mysql-connector-java'
    compile 'com.google.cloud.sql:mysql-socket-factory:1.0.4'
    compile 'org.flywaydb:flyway-core'
    compile 'com.google.guava:guava:19.0'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'com.google.appengine:appengine:+'
    compile 'org.jooq:jooq'
    compile 'io.swagger:swagger-annotations:1.5.17'


    jooqRuntime 'mysql:mysql-connector-java:6.0.6'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'junit:junit:4.12'
}

flyway {
    url = 'jdbc:mysql://localhost:3306/book_db?useSSL=false&serverTimezone=Europe/Moscow'
    user = 'root'
    password = '123'
    schemas = ['book_db']
    locations = ["filesystem:$project.projectDir/src/main/resources/sql"]
    sqlMigrationPrefix = 'v'
    sqlMigrationSeparator = '__'
    sqlMigrationSuffixes = ['.sql']
}

jooq {
    version = '3.10.1'
    edition = 'OSS'
    compile(sourceSets.main) {
        jdbc {
            driver = 'com.mysql.cj.jdbc.Driver'
            url = 'jdbc:mysql://localhost:3306/book_db?useSSL=false&serverTimezone=Europe/Moscow'
            user = 'root'
            password = '123'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.mysql.MySQLDatabase'
                inputSchema = 'book_db'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'com.oneuse.dainbow.books.application.jooq'
            }
        }
    }
}

generateCompileJooqSchemaSource.dependsOn flywayMigrate

// Google Cloud Platform
appengine {

    deploy {   // deploy configuration
        stopPreviousVersion = true  // default - stop the current version
        promote = true              // default - & make this the current version
    }
}